import info.metadude.kotlin.library.engelsystem.Libs
import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent
import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

apply plugin: "java-library"
apply plugin: "kotlin"
apply plugin: "com.google.devtools.ksp"

compileJava {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(KotlinCompile).configureEach {
    compilerOptions {
        jvmTarget = JvmTarget.JVM_17
    }
}

dependencies {
    implementation Libs.kotlinCoroutinesCore
    implementation Libs.moshi
    ksp Libs.moshiKotlinCodegen
    implementation Libs.okhttp
    implementation Libs.okhttpLoggingInterceptor
    api Libs.retrofit
    implementation Libs.retrofitMoshi
    api Libs.threetenbp

    testImplementation Libs.truth
    testImplementation Libs.junitJupiterApi
    testRuntimeOnly Libs.junitJupiterEngine
    testRuntimeOnly Libs.junitPlatformLauncher
    testImplementation Libs.kotlinCoroutinesTest
}

test {
    useJUnitPlatform()
    testLogging {
        events TestLogEvent.FAILED,
                TestLogEvent.PASSED,
                TestLogEvent.SKIPPED,
                TestLogEvent.STANDARD_ERROR,
                TestLogEvent.STANDARD_OUT
        exceptionFormat TestExceptionFormat.FULL
        showCauses true
        showExceptions true
        showStackTraces true
    }
}

apply from: rootProject.file("gradle/deploy.gradle")
