apply plugin: "java"
apply plugin: "signing"
apply plugin: "maven-publish"
apply plugin: "org.jetbrains.dokka-javadoc"

// javadocJar and sourcesJar tasks pick up group and version
group = project.groupId
version = project.versionName

def moduleArtifactId = project.hasProperty('moduleArtifactId') ? project.moduleArtifactId : project.artifactId

tasks.register("sourcesJar", Jar) {
    group "publishing"
    description "Generates source jar"
    archiveClassifier.set("sources")
    from kotlin.sourceSets.main.kotlin.srcDirs
}

tasks.register("javadocJar", Jar) {
    dependsOn dokkaGeneratePublicationJavadoc
    group "publishing"
    description "Generates javadocJar based on Dokka"
    archiveClassifier.set("javadoc")
    from dokkaGeneratePublicationJavadoc.outputDirectory
}

dokka {
    moduleName.set(project.name)
    dokkaSourceSets.main {
        sourceLink {
            // Get the module path for source linking
            def modulePath = project.path.replace(':', '')
            localDirectory.set(file("${modulePath}/src/main/kotlin"))
            remoteUrl.set(URI.create("https://github.com/johnjohndoe/engelsystem/blob/master/${modulePath}/src/main/kotlin"))
            remoteLineSuffix.set("#L")
        }
    }
}

artifacts {
    archives javadocJar
}

Properties properties = new Properties()
def propertiesFile = project.rootProject.file("${System.properties['user.home']}/.gradle/gradle.properties")
if (propertiesFile.exists()) {
    properties.load(propertiesFile.newDataInputStream())
}
def mavenCentralUrl = properties.getProperty("sonatypeRepo") ?: project.sonatypeRepo
def mavenCentralUserName = properties.getProperty("sonatypeUserName") ?: project.sonatypeUserName
def mavenCentralPassword = properties.getProperty("sonatypePassword") ?: project.sonatypePassword

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.java
                groupId = project.groupId
                artifactId = moduleArtifactId
                version = project.versionName

                artifact sourcesJar {
                    archiveClassifier = "sources"
                }

                artifact javadocJar {
                    archiveClassifier = "javadoc"
                }

                pom {
                    name = project.packageName
                    description = project.packageDescription
                    url = project.packageWebsiteUrl
                    licenses {
                        license {
                            name = project.packageLicenseApacheName
                            url = project.packageLicenseApacheUrl
                        }
                    }
                    scm {
                        connection = project.packageScmConnection
                        developerConnection = project.packageScmDeveloperConnection
                        url = project.packageWebsiteUrl
                    }
                    developers {
                        developer {
                            id = packageDeveloperId
                            name = packageDeveloperName
                            email = packageDeveloperEmail
                        }
                    }
                }

                repositories {
                    maven {
                        name project.mavenCentralPublicationName
                        url = uri(mavenCentralUrl)
                        credentials {
                            username = mavenCentralUserName
                            password = mavenCentralPassword
                        }
                    }
                }
            }
        }
    }
    signing {
        required { gradle.taskGraph.hasTask("publishReleasePublicationToMavenCentralRepository") }
        sign publishing.publications.release
    }
}

tasks.named("dokkaGeneratePublicationJavadoc").configure {
    if (project.plugins.hasPlugin("com.google.devtools.ksp")) {
        dependsOn("kspKotlin")
    }
}

tasks.named("sourcesJar").configure {
    if (project.plugins.hasPlugin("com.google.devtools.ksp")) {
        dependsOn("kspKotlin")
    }
}
